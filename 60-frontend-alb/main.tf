# Creation of Load Balancer (Frontend ALB)
resource "aws_lb" "frontend_alb" {
  name               = "${var.project}-${var.environment}-frontend" # roboshop-dev-frontend
  internal           = false # Because it is public
  load_balancer_type = "application"
  security_groups    = [local.frontend_alb_sg_id]
  subnets            = local.public_subnet_ids

  # Keeping it as false, just to delete using terraform while practice
  enable_deletion_protection = false

  # roboshop-dev-frontend
  tags = merge(
    {
        Name = "${var.project}-${var.environment}-frontend"
    },
    local.common_tags
  )
}

# Listener (HTTP/HTTPS) & SSL(Secure Sockets Layer) policy is required for using HTTPS protocol
resource "aws_lb_listener" "https" {
  load_balancer_arn = aws_lb.frontend_alb.arn
  port              = "443"
  protocol          = "HTTPS"
  ssl_policy        = "ELBSecurityPolicy-2016-08"
  certificate_arn   = local.frontend_alb_certificate_arn

  default_action {
    type = "fixed-response"

    fixed_response {
      content_type = "text/html"
      message_body = "<h1>Hi, I am from HTTPS Frontend ALB</h1>"
      status_code  = "200"
    }
  }
}

# Route53 record
resource "aws_route53_record" "www" {
  zone_id   = var.zone_id
  name      = "*.${var.domain_name}"   # *.devopsdaws.online
  type      = "A"

  # Load Balancer details
  alias {
    name                   = aws_lb.frontend_alb.dns_name   # dualstack.frontend-alb-dev.us-east-1.elb.amazonaws.com
    zone_id                = aws_lb.frontend_alb.zone_id
    evaluate_target_health = true
  }
  allow_overwrite = true
}